// This Pine Scriptâ„¢ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© InNoobWeTrust

//@version=5

// @description Calculations and structures around MA lines
library("ma_lib")

//===============================================================================
// Types
//===============================================================================

export enum MaT
	sma = 'SMA'
	wma = 'WMA'
	ema = 'EMA'

export type MaStructure
	float src
	float ma_fast
	float ma_slow

//===============================================================================
// Functions
//===============================================================================

// @function calculate moving average
// @param t type of moving average
// @param src the series to calculate
// @param len averaging length
// @returns the moving average series
export ma(MaT t, float src, simple int len) =>
	switch t
		MaT.sma => ta.sma(src,len)
		MaT.wma => ta.wma(src,len)
		MaT.ema => ta.ema(src,len)

// @function calculate market structure based on source (OHLC) and 2 MA lines
// @param src source value (OHLC)
// @param ma_fast faster MA line
// @param ma_slow slower MA line
export market_structure(MaStructure s) =>
	src = s.src
	ma_fast = s.ma_fast
	ma_slow = s.ma_slow
    src_vs_fast = math.sign(src - ma_fast)
    fast_vs_slow = math.sign(ma_fast - ma_slow)
    mul_sign = math.sign(src_vs_fast * fast_vs_slow)
    sum_sign = math.sign(src_vs_fast + fast_vs_slow)
	// Return 1 if up, -1 if down, 0 otherwise
	mul_sign * sum_sign

// @function calculate market structure of a pair of MA lines
export pair_structure(float lower_ma, float higher_ma) =>
	ta.valuewhen(ta.cross(lower_ma, higher_ma), math.sign(lower_ma - higher_ma), 0)

// @function calculate the synchronization of 2 structures from 4 MA lines
export sync_structure(MaStructure lower, MaStructure higher) =>
    trend = market_structure(lower)
    higher_trend = market_structure(higher)
	mutual_trend = pair_structure(lower.ma_slow, higher.ma_slow)

    if trend > 0 and higher_trend > 0 and mutual_trend > 0
        1
    else if trend < 0 and higher_trend < 0 and mutual_trend < 0
        -1
    else
        0
