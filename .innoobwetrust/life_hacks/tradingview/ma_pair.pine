// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © forbidden_gun

//@version=6
indicator("Moving Average Pair",overlay = true)
import forbidden_gun/ma_lib/3

//===============================================================================
// Inputs
//===============================================================================

fast_grp = '► Fast MA'
fast_type = input.enum(ma_lib.MaT.sma,'MA type',group=fast_grp)
fast_src = input.source(close,'Source',group=fast_grp)
fast_len = input.int(9,'Len',group=fast_grp)
fast_color = input.color(color.new(color.blue,50),'Color',group=fast_grp)
fast_linewidth = input.int(1,'Linewidth',group=fast_grp)

slow_grp = '► Slow MA'
slow_type = input.enum(ma_lib.MaT.sma,'MA type',group=slow_grp)
slow_src = input.source(close,'Source',group=slow_grp)
slow_len = input.int(16,'Len',group=slow_grp)
slow_color = input.color(color.new(color.orange,50),'Color',group=slow_grp)
slow_linewidth = input.int(2,'Linewidth',group=slow_grp)

cross_grp = '► Crossing'
cross_highlight = input.string('Circle','Highlight type',options = ['Background','Circle','None'], group = cross_grp)
cross_up_color = input.color(color.new(color.green,70),'Up color',group = cross_grp)
cross_dwn_color = input.color(color.new(color.red,70),'Down color',group = cross_grp)

//===============================================================================
// Calculations
//===============================================================================

// MA pair
fast_ma = ma_lib.ma(fast_type, fast_src, fast_len)
slow_ma = ma_lib.ma(slow_type, slow_src, slow_len)

// Crossing signals
cross_up_signal = ta.crossover(fast_ma,slow_ma)
cross_dwn_signal = ta.crossunder(fast_ma,slow_ma)
cross_loc = cross_up_signal or cross_dwn_signal ? fast_ma[1] : na
cross_color = cross_up_signal ? cross_up_color : cross_dwn_signal ? cross_dwn_color : color(na)

cross_bg_color = switch cross_highlight
    'Background' => cross_color
    => color(na)

//===============================================================================
// Plot
//===============================================================================

// MA pair
plot(fast_ma,'Fast MA',color=fast_color,linewidth=fast_linewidth)
plot(slow_ma,'Slow MA',color=slow_color,linewidth=slow_linewidth)

// Crossing background
bgcolor(cross_bg_color)

// Circles
plotshape(cross_highlight == 'Circle' ? cross_loc : na, 'Cross circle highlight',style=shape.circle,location = location.absolute,color = cross_color,size = size.normal)